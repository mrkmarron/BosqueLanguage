//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity KeyList<K where KeyType> provides Object {
    field hkey: K;
    field tail: KeyList<K>?;

    hidden recursive static _reverse(kl: KeyList<K>, rkl: KeyList<K>): KeyList<K> {
        let tl = kl.tail;
        if(tl == none) {
            return KeyList<K>@{hkey=kl.hkey, tail=rkl};
        }
        else {
            return KeyList<K>::_reverse[recursive](tl, KeyList<K>@{hkey=kl.hkey, tail=rkl});
        }
    }

    static reverse(kl: KeyList<K>?): KeyList<K>? {
        if(kl == none) {
            return none;
        }
        else {
            let tl = kl.tail;
            if(tl == none) {
                return kl;
            }
            else {
                return KeyList<K>::_reverse[recursive](tl, KeyList<K>@{hkey=kl.hkey, tail=none});
            }
        }
    }
}